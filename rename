#!/usr/bin/perl

# From Programming Perl, Page 312
# Usage: rename perlexpr [filenames]
# Examples:
# 1. To strip the .bak extension from a bunch of files:
#   rename 's/\.bak$//' *.bak
# 2. To add the .bak extension back on (2 ways):
#   rename '$_ .= ".bak"' *
#   rename 's/$/.bak/' *
# 3. To change extensions from .bak to .txt:
#   rename 's/\.bak$/\.txt/' *.bak
# 4. To translate uppercase to lowercase (or vice versa):
#   rename 'tr/A-Z/a-z/' *
#   rename 'tr/a-z/A-Z/' *

($op = shift) || die "Usage: rename perlexpr [filenames]\n";

if (!@ARGV) {
  @ARGV = <STDIN>;
  chop(@ARGV);
}

for (@ARGV) {
  $was = $_;
  eval $op;
  die $@ if $@;
  rename ($was, $_) unless $was eq $_;
}
